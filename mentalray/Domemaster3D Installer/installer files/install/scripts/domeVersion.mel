/*
	Dome Version Checker V1.0
	by Andrew Hazelden

	This tool displays the version number and lets you download an update.

	Version History
	----------------
  
  Domemaster3D V1.4 B4 - Oct 21, 2013
  Domemaster3D V1.4 B3 - Oct 8, 2013
  Domemaster3D V1.4 B2 - Oct 6, 2013

	To use the Dome Version Checker script run the following MEL commands:
	print("Dome Version Checker");
	source "domeVersion.mel";
	domeVersion();

*/


//Check what Maya version is active
global proc int getMayaVersion(){
  // Check if we are running Maya 2011 or higher
  float $mayaVersion = `getApplicationVersionAsFloat`;

  // Test this GUI using the Maya 2010 - non-docked GUI mode
  //float $mayaVersion = 2010;

  // Write out the current Maya version number
  //print("Maya " + $mayaVersion + " detected.\n");

  return $mayaVersion;
}

  
//Check the operating system
global proc string getMayaPlatform(){
  //-------------------------------------------------
  //Get the current Maya Platform using python
  //      The $mayaPlatform options are:
  //          "windows", "darwin", "linux"
  //-------------------------------------------------

  string $mayaPlatform = "";
  python( "import sys" );

  if( python( "sys.platform == 'win32'" ) ){
    $mayaPlatform = "windows";
  }else if( python( "sys.platform == 'windows'" ) ){
    $mayaPlatform = "windows";
  }else if( python( "sys.platform == 'darwin'" ) ){
    $mayaPlatform = "darwin";
  }else {
    $mayaPlatform = "linux";
  }

  //print("Running on " + $mayaPlatform + ".\n");
  return $mayaPlatform;
}


//Show the version info window
global proc domeVersion(){
  string $dialogueButtonSelected = "";
  string $domemaster3D_version = "1.4 B5";
  string $release_date = "Oct 24, 2013";

  string $domeDialgueString = "";
  $domeDialgueString += "Domemaster3D Shader for Maya" + " - Version " + $domemaster3D_version + "\n";
  $domeDialgueString += "This edition was released on " + $release_date + ".\n";
  $domeDialgueString += "\n";
  $domeDialgueString += "A 2D/3D angular fisheye rendering system for planetariums and fulldome theatres.\n";
  $domeDialgueString += "This shader is also available for 3DS Max, and Softimage.\n";
  $domeDialgueString += "\n";
  $domeDialgueString += "The Domemaster3D shader is distributed under the GPL v3 license.\n";
  
  // Check if we are running Maya 2011 or higher
  float $mayaVersion = getMayaVersion();
  
  if ($mayaVersion >=2011){
    //Maya 2011 or newer detected
    $dialogueButtonSelected = `confirmDialog 
      -title "About Domemaster3D for Maya"
      -messageAlign center
      -message $domeDialgueString
      -icon "information"
      -button "Check for Update"
      -button "Read the User Forum"
      -button "Report a Bug"
      -button "Close"
      -defaultButton "Close"`;
  } else {
    //Maya 2010 or older detected
    $dialogueButtonSelected = `confirmDialog 
      -title "About Domemaster3D for Maya"
      -messageAlign center
      -message $domeDialgueString
      -button "Check for Update"
      -button "Read the User Forum"
      -button "Report a Bug"
      -button "Close"
      -defaultButton "Close"`;
  }

  if($dialogueButtonSelected == "Check for Update"){
    python("import domeCamera as domeCamera\ndomeCamera.openDomemasterDownloads()"); 
  }else if($dialogueButtonSelected == "Read the User Forum"){
    python("import domeCamera as domeCamera\ndomeCamera.openDomemasterNing()"); 
  }else if($dialogueButtonSelected == "Report a Bug"){
    python("import domeCamera as domeCamera\ndomeCamera.openDomemasterBugReport()"); 
  }

}

