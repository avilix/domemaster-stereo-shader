//  Version 1.4 - Dec 18, 2013
//
//  Procedure Name:
//	AElatlong_lensTemplate
//
//  Description:
//	Creates the attribute editor controls for the latlong_lens shader
//
//  Version History
//  -----------------
//
//	Version 1.4 - Dec 18, 2013 
// Initial Release 
//
// Copied base AE template file from domeAFL_FOV_Stereo node
//
//
//	Author:
//	Andrew Hazelden
//
//	Contact:
//	andrew@andrewhazelden.com
//


//---------------------------------------------------------------------------------
// New GUI Controls
//---------------------------------------------------------------------------------

//Return the name of the Node's Attr
global proc string dome_AEStripNodeName (string $nodeName)
{
	string $buffer[];
	tokenize $nodeName "." $buffer;
	return $buffer[1];
}


//Return the base name of the Node
global proc string dome_AEStripAttrName (string $nodeName)
{
	string $buffer[];
	tokenize $nodeName "." $buffer;
	return $buffer[0];
}


//Find the shape node
//example: print `getObjectShapeNode( "rob_lookup_background_Camera1")`;
global proc string getObjectShapeNode ( string $object ) {
  string $shapes[] = `listRelatives -children -shapes $object`;
  return $shapes[0];
}


//Find the object's parent node
//example: print `getObjectParentNode( "rob_lookup_background_CameraShape2")`;
global proc string getObjectParentNode ( string $object ) {
  string $shapes[] = `listRelatives -parent -shapes $object`;
  return $shapes[0];
}


//This creates the controls in the attribute editor
global proc AElatlong_lens_AddDynamicAttrs( string $nodeName )
{
  //separate the node name from the node.attribute name
  string $baseNodeName = dome_AEStripAttrName($nodeName);
  
  int $NewDomeSetup = 0;
  
  //Add a version number attribute to the extra attribute fields
  string $attrName ="domeVersion";
  int $domeVersion = 1;
  if (!`attributeExists $attrName $baseNodeName`) {
    addAttr -ln $attrName -at long -min 0 $baseNodeName;
    setAttr ($baseNodeName+"."+$attrName) $domeVersion;
    print("Adding custom attribute " + $baseNodeName +"." +$attrName + "\n");
    
    $NewDomeSetup = 1;
    
    //Add the Attribute Editor node "help menu" link
    addAttributeEditorNodeHelp(
      $baseNodeName,
      "showHelp -absolute \"https://code.google.com/p/domemaster-stereo-shader/w/list\"");
  }
}


//Create the attribute editor view for the latlong_lens node
global proc AElatlong_lensTemplate( string $nodeName )
{
	AEswatchDisplay $nodeName;
	editorTemplate -beginScrollLayout;

    editorTemplate -beginLayout "LatLong Shader" -collapse 0;
      editorTemplate 	-beginNoOptimize;
      
        //Add the custom dynamic attributes to the node
        editorTemplate -callCustom "AElatlong_lens_AddDynamicAttrs" "AElatlong_lens_AddDynamicAttrs" "flipHorizontal";
          
        //Define the image orientation attributes
        editorTemplate -beginLayout "Image Orientation" -collapse 0;
          editorTemplate 	-beginNoOptimize;
          
          editorTemplate -label "Flip Horizontal" -annotation "Flips the image horizontally" -addControl "flipHorizontal";	
          
          editorTemplate -endNoOptimize;
        editorTemplate -endLayout;

      editorTemplate -endNoOptimize;
    editorTemplate -endLayout;


    AEdependNodeTemplate $nodeName;


    //Hide the raw fields
    editorTemplate -suppress "caching";
    editorTemplate -suppress "nodeState";
    
    //Add any new controls that aren't listed above to the bottom of the attribute editor window 
    editorTemplate -addExtraControls;
    
  editorTemplate -endScrollLayout;
}
